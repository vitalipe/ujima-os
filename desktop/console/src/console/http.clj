(ns console.http
  "The console's HTTP edge (http-kit). Transport only:
     POST /circle/<verb>       circle's command tier -> 202 {:job id}
     POST /setup/<op>          setup's commands (jobs) + panel ops (sync)
     POST /update/<op>         update's jobs — this computer only
     POST /trial-boot/<op>     the trial-boot page's decision — this computer only
     POST /console/rescan      sweep the subnet — console-plane, either panel
     GET  /ui/<panel>          the composed views the panels poll
     GET  /console/job[/<id>]  the shared job inspection tier
   plus statics: / the chooser, /<panel>/* the panels."
  (:require [clojure.string     :as str]
            [clojure.java.io    :as io]
            [org.httpkit.server :as http]
            [lib.edn            :refer [edn->json json->edn]]
            [console.jobs          :as jobs]
            [console.circle        :as circle]
            [console.panel.circle  :as circle-panel]
            [console.panel.setup   :as setup-panel]
            [console.panel.update  :as update-panel]
            [console.panel.tryboot :as tryboot-panel]))


(defn- json [status body]
  {:status status :headers {"content-type" "application/json"} :body (edn->json body)})

(defn- body-edn
  "The request body as edn; a body that isn't JSON is the caller's fault, not a 500."
  [req]
  (try (json->edn (:body req))
       (catch Exception _
         (throw (ex-info "body must be JSON" {:error :request/malformed})))))


(def ^:private content-types {"html" "text/html; charset=utf-8"
                              "css"  "text/css"
                              "js"   "text/javascript"})

(def ^:private ui-files
  {"circle"     #{"index.html" "circle.css" "circle.js"}
   "setup"      #{"index.html" "setup.css" "setup.js"}
   "update"     #{"index.html" "update.css" "update.js"}
   "trial-boot" #{"index.html" "trial-boot.css" "trial-boot.js"}})

(defn- serve-file [root name]
  (let [f   (io/file root name)
        ext (some-> (re-find #"\.([^.]+)$" name) second)]
    (when (.isFile f)
      {:status  200
       :headers {"content-type" (content-types ext "application/octet-stream")}
       :body    f})))

(defn- static-file
  "/ -> the chooser; /circle[/<file>] and /setup[/<file>] -> that panel's
   files; whitelist only, so no escapes."
  [ui-roots parts]
  (cond
    (empty? parts)
    (serve-file (:console ui-roots) "index.html")

    (contains? ui-files (first parts))
    (let [[app file] parts
          name       (or file "index.html")]
      (when (and (<= (count parts) 2) ((ui-files app) name))
        (serve-file (get ui-roots (keyword app)) name)))))


(defn- setup-route [op body]
  (case op
    "settings" (json 202 {:job (setup-panel/settings-job! body)})
    "clock"    (json 202 {:job (setup-panel/clock-job! body)})
    "restart"  (json 202 {:job (setup-panel/power-job! :restart body)})
    "poweroff" (json 202 {:job (setup-panel/power-job! :poweroff body)})
    "remove"   (json 200 (setup-panel/remove! body))
    nil))

(defn- update-route [op body]
  (case op
    "install" (json 202 {:job (update-panel/install-job! body)})
    "revert"  (json 202 {:job (update-panel/revert-job! body)})
    nil))

(defn- trial-boot-route [op]
  (case op
    "keep" (json 202 {:job (tryboot-panel/keep-job!)})
    "back" (json 202 {:job (tryboot-panel/back-job!)})
    nil))

(defn- handler [{:keys [ui-roots]} req]
  (try
    (let [method (:request-method req)
          parts  (->> (str/split (str (:uri req)) #"/") (remove str/blank?) vec)]
      (cond
        (and (= :post method) (circle-panel/verb-routes parts))
        (let [verb (circle-panel/verb-routes parts)
              body (body-edn req)]
          (json 202 {:job (circle-panel/act! verb body)}))

        (and (= :post method) (= 2 (count parts)) (= "setup" (first parts)))
        (or (setup-route (second parts) (body-edn req))
            (json 404 {:error "not found"}))

        (and (= :post method) (= 2 (count parts)) (= "update" (first parts)))
        (or (update-route (second parts) (body-edn req))
            (json 404 {:error "not found"}))

        (and (= :post method) (= 2 (count parts)) (= "trial-boot" (first parts)))
        (or (trial-boot-route (second parts))
            (json 404 {:error "not found"}))

        (and (= :get method) (= ["ui" "circle"] parts))
        (json 200 (circle-panel/view))

        (and (= :get method) (= ["ui" "setup"] parts))
        (json 200 (setup-panel/view))

        (and (= :get method) (= ["ui" "update"] parts))
        (json 200 (update-panel/view))

        (and (= :get method) (= ["ui" "trial-boot"] parts))
        (json 200 (tryboot-panel/view))

        (and (= :post method) (= ["console" "rescan"] parts))
        (json 200 (circle/rescan!))

        (and (= :get method) (= ["console" "job"] parts))
        (json 200 {:jobs (jobs/jobs)})

        (and (= :get method) (= 3 (count parts)) (= ["console" "job"] (subvec parts 0 2)))
        (if-let [job (some-> (nth parts 2) parse-long jobs/job)]
          (json 200 job)
          (json 404 {:error "unknown job"}))

        (= :get method)
        (or (static-file ui-roots parts)
            (json 404 {:error "not found"}))

        :otherwise
        (json 404 {:error "not found"})))
    (catch clojure.lang.ExceptionInfo e
      (if (= :request/malformed (:error (ex-data e)))
        (json 400 {:error (ex-message e)})
        (do (println "console http: handler failed:" (ex-message e))
            (json 500 {:error "internal error"}))))
    (catch Throwable e
      (println "console http: handler failed:" (ex-message e))
      (json 500 {:error "internal error"}))))


(defn init!
  "Starts the server; returns http-kit's stop fn. A taken port throws."
  [{:keys [host port ui-roots] :or {host "127.0.0.1" port 1338}}]
  (http/run-server (partial handler {:ui-roots ui-roots})
                   {:ip host :port port}))
