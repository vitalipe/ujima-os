(ns schema.ujima.api.query
  "The shapes /api/query answers with — the frozen v1 contract. Maps are OPEN: a new key is legal,
   a renamed or retyped one fails the contract test.")


;; one def, so a reply and the machine tree cannot disagree
(def audio
  [:map
   [:volume [:maybe [:int {:min 0, :max 100}]]]
   [:muted  :boolean]
   [:output [:maybe [:enum :usb :hdmi]]]])


;; :scopes holds the allowed ones only — the keys are the write whitelist
(def settings-record
  [:map
   [:effective :any]
   [:via       [:enum :circle :device :session :activity :default]]
   [:default   :any]
   [:scopes    [:map-of [:enum :circle :device :session :activity] :any]]])


(def machine
  (let [desktop-entry   [:map [:id :keyword] [:label [:maybe :string]] [:category [:maybe :keyword]]]
        partition-space [:map [:total-mb :int] [:free-mb :int]]
        interface       [:map [:up :boolean]
                              [:ip [:maybe :string]]
                              [:prefix [:maybe :int]]
                              [:mac [:maybe :string]]
                              [:gateway [:maybe :string]]
                              [:dhcp :boolean]]]

    [:map
     [:schema   [:= 1]]
     [:id       [:maybe :string]] ;; FIXME-nil until the card-stamped id lands
     [:device   [:map [:serial [:maybe :string]] [:model [:maybe :string]]]]
     [:image    [:map [:version [:maybe :string]]]]
     [:disk     [:map [:type     [:maybe :keyword]]
                      [:slot     [:maybe :keyword]]
                      [:storage  [:maybe partition-space]]
                      [:settings [:maybe partition-space]]]]
     [:places   [:vector [:map [:id [:tuple :keyword :string]]
                               [:kind :keyword]
                               [:name :string]
                               [:state :keyword]
                               [:mount {:optional true} :string]
                               [:storage {:optional true} :string]
                               [:tokens {:optional true} [:vector :string]]]]]
     [:desktop  [:map [:locked  [:maybe :boolean]]
                      [:mode    [:map [:mode [:enum "multi" "solo" "locked"]]
                                      [:app {:optional true} :string]]]
                      [:running [:maybe desktop-entry]]
                      [:catalog [:vector desktop-entry]]]]
     [:audio    audio]
     [:keyboard [:map [:layout :string] [:available-layouts [:vector :string]]]]
     [:net      [:map [:ip [:maybe :string]]
                      [:interfaces [:map-of :keyword interface]]]]
     [:system   [:map [:name     [:maybe :string]]
                      [:timezone [:maybe :string]]
                      [:clock-ms :int]]]
     [:health   [:map [:uptime-minutes [:maybe :int]]
                      [:messages [:vector [:map [:type :keyword] [:id :keyword] [:label :string]]]]]]]))
