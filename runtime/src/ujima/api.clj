(ns ujima.api
  "The /api tier: query/machine a node per source, query/settings one node over control's
   records, commands the verbs. The contract — docs, params, errors, reply shapes — is pure
   data in schema.ujima.api.*; this ns binds an effect to each verb."
  (:require [schema.ujima.api.commands :as defs]
            [ujima.api.routes       :as routes]
            [ujima.control          :as control]
            [ujima.control.queries  :as queries]
            [ujima.control.commands :as effects]
            [ujima.desktop.app      :as desktop]
            [ujima.desktop.places   :as places]
            [ujima.storage          :as storage]
            [ujima.linux.devicetree :as devicetree]
            [ujima.linux.disk       :as disk]
            [ujima.linux.net        :as net]
            [ujima.linux.system     :as system]))


;; ── the verbs ───────────────────────────────────────────────────────────────

(defn- with-handlers
  "Each spec bound to this tier's handler. A spec with no handler, or a handler with no
   spec, fails here — at load — rather than at request time."
  [specs handlers]
  (assert (= (set (keys specs)) (set (keys handlers)))
          (str "command table drift — spec with no handler: " (sort (remove handlers (keys specs)))
               ", handler with no spec: "                     (sort (remove specs (keys handlers)))))
  (into {} (for [[path spec] specs] [path (assoc spec :handler (handlers path))])))


(def commands
  (with-handlers defs/commands
    {"app/open"     (fn [{:keys [app]}] (desktop/run! (keyword app)))
     "app/switch"   (fn [{:keys [app]}] (desktop/switch-to! (keyword app)))
     ;; the circle closes THROUGH a hold: let go first, then close — but never a lock
     "app/close"    (fn [_] (desktop/refuse-when-locked!)
                            (desktop/release!)
                            (desktop/close-focused!))
     "app/home"     (fn [_] (desktop/go-home!))
     "app/open-url" (fn [{:keys [url]}] (desktop/open-url! url))

     "audio/volume"    (fn [{:keys [scope value]}]  (effects/change-current-volume! value scope))
     "keyboard/layout" (fn [{:keys [scope layout]}] (effects/change-keyboard-layout! layout scope))

     "settings/**"     (fn [{:keys [path value scope]}] (effects/change-setting! path value scope))

     "clear/:scope/**" (fn [{:keys [scope path]}]
                         (if (seq path)
                           (effects/clear-setting! path scope)
                           (effects/clear-scope! scope)))

     ;; timezone first: a bad zone is refused before the clock moves
     "system/clock"    (fn [{:keys [epoch timezone]}]
                         (when timezone
                           (effects/change-setting! [:system :timezone] timezone :device))
                         (system/clock! epoch)
                         (effects/change-setting! [:system :clock :epoch-floor] epoch :device))

     "desktop/focus"   (fn [{:keys [app]}]
                         (if app
                           (desktop/solo-app! (keyword app))
                           (desktop/solo-current-app!)))

     "desktop/release" (fn [_] (desktop/refuse-when-locked!)
                               (desktop/release!))

     "desktop/lock"    (fn [_] (desktop/lock!))
     "desktop/unlock"  (fn [_] (desktop/unlock!))

     "system/restart"  (fn [_] (system/reboot!))
     "system/poweroff" (fn [_] (system/shutdown!))}))


;; ── the routes ──────────────────────────────────────────────────────────────

(defn machine-nodes
  "The open machine tier, one node per source. A var, not a literal inside endpoints, so
   the sim can hold its fake tier to this one's shape."
  [{:keys [version id slot] system-disk :disk}]
  {"schema"   (constantly 1)
   "id"       (constantly id)
   "device"   (fn [] {:serial (devicetree/serial)
                      :model  (devicetree/model)})
   "image"    (constantly {:version version})
   
   "disk"     (fn [] {:type     (:type system-disk)
                      :slot     slot
                      :storage  (disk/device->space (:storage system-disk))
                      :settings (disk/device->space (get-in system-disk [:slots slot :config]))})

   ;; the same blob the desktop's places stream carries: roots and token types, never values
   "places"   (fn [] (:places (places/places->ui (storage/snapshot))))

   "desktop/locked"  #(desktop/locked?)
   "desktop/mode"    #(desktop/mode-state)
   "desktop/running" #(:current (desktop/current-apps-state))
   "desktop/catalog" (fn []
                       (->> (desktop/catalog-listing)
                         (remove :hidden)
                         (mapv   #(dissoc % :hidden))))

   "audio"    #(queries/audio-status (control/settings))

   "keyboard" (fn [] (let [s (control/settings)]
                       {:layout            (:effective (get s [:keyboard :layout]))
                        :available-layouts (:effective (get s [:keyboard :available-layouts]))}))

   "net"      (fn [] (let [facts (net/interface-facts)]
                       {:ip         (net/lan-ip facts)
                        :interfaces facts}))

   "system/name"     #(:effective (control/setting [:system :name]))
   "system/timezone" #(:effective (control/setting [:system :timezone]))
   "system/clock-ms" #(System/currentTimeMillis)

   "health/uptime-minutes" system/uptime-minutes
   "health/messages"       (constantly [])})


(defn endpoints
  [{:keys [gate] :as opts}]
  (assert gate "no :gate — pass identity to serve unauthenticated")

  {:errors defs/errors
   :routes

   (merge

     ;; gated
     (gate
      (routes/commands
       {:base     "commands"
        :commands commands}))

     (gate
      (routes/queries
       {:base  "query"
        :nodes {"settings" #(-> (control/settings)
                              (queries/public-settings)
                              (queries/settings->tree))}}))
     ;; open
     (routes/queries
      {:base  "query/machine"
       :nodes (machine-nodes opts)}))})
