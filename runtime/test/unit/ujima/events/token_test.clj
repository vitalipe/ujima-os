(ns ujima.events.token-test
  (:require [clojure.test :refer [deftest is testing use-fixtures]]
            [babashka.fs         :as fs]
            [ujima.control       :as control]
            [ujima.desktop.app   :as app]
            [ujima.desktop.app.catalog :as catalog]
            [ujima.linux.systemd :as systemd]
            [ujima.events.token :as token]
            [ujima.events.tryboot :as tryboot]))


(defn- mounted [& [tokens]]
  [{:uuid "U" :state :mounted :mount "/ujima/run/storage/U" :tokens (or tokens {})}])


(def ^:private secret {:circle/secret {:key "abc" :circle "room-1"}})

;; the stick is only a token when its key is the one THIS machine holds
(defn- circle-holds! [token]
  (let [dir (str (fs/create-temp-dir))]
    (control/init! {:storage dir :tmp dir})
    (control/settings! :circle [:circle :token] token)))

(use-fixtures :each (fn [run] (circle-holds! "abc") (run)))


;; --- what counts as a token -------------------------------------------------

(deftest a-circle-marker-with-a-key-is-the-token
  (is (= "abc" (token/circle-token (mounted secret)))))


(deftest nothing-is-not-a-token
  (is (nil? (token/circle-token nil))       "no previous push at all")
  (is (nil? (token/circle-token [])))
  (is (nil? (token/circle-token (mounted))) "a stick with no markers")
  (is (nil? (token/circle-token (mounted {:circle/secret nil})))
      "storage validates and skips junk now, so nil cannot arrive — the consumer stays defensive anyway")
  (is (nil? (token/circle-token (mounted {:circle/secret {:circle "room-1"}})))
      "parsed but no :key")
  (is (nil? (token/circle-token (mounted {:circle/secret {:key "   "}})))
      "blank key"))


;; --- the edge ---------------------------------------------------------------

(deftest transitions
  (testing "arrival opens"
    (is (= :open (token/transition nil "abc"))))

  (testing "a first push has no before, so a stick already in at boot reads as arrival"
    (is (= :open (token/transition (token/circle-token nil) "abc"))))

  (testing "unchanged does nothing — level-triggering here would yank the workspace"
    (is (nil? (token/transition "abc" "abc"))))

  (testing "departure closes"
    (is (= :close (token/transition "abc" nil))))

  (testing "absent stays absent"
    (is (nil? (token/transition nil nil))))

  (testing "a swapped stick with a different key is an arrival, not a no-op"
    (is (= :open (token/transition "abc" "xyz")))))


;; --- effects ----------------------------------------------------------------

(deftest opening-hands-the-console-the-key-then-runs-it
  (let [calls* (atom [])]
    (with-redefs [systemd/active?    (constantly false)
                  app/release! (fn [] nil)
                  catalog/merge-app! (fn [id changes] (swap! calls* conj [:update id changes]))
                  app/run!           (fn [id]     (swap! calls* conj [:run id]))]
      (is (= :open (token/on-storage! (mounted secret) nil)))
      (is (= [[:update :console {:env {"UJIMA_CIRCLE_TOKEN" "abc"} :hidden false}]
              [:run :console]]
             @calls*)
          "env + unhide must land BEFORE run! — the launch captures the entry"))))


(deftest in-a-trial-boot-the-console-is-pinned-but-never-opened
  (let [calls* (atom [])]
    (with-redefs [systemd/active?    (constantly false)
                  tryboot/trial?     (constantly true)
                  app/release! (fn [] nil)
                  catalog/merge-app! (fn [id changes] (swap! calls* conj [:update id changes]))
                  app/run!           (fn [id]     (swap! calls* conj [:run id]))]
      (is (= :open (token/on-storage! (mounted secret) nil)))
      (is (= [[:update :console {:env {"UJIMA_CIRCLE_TOKEN" "abc"} :hidden false}]]
             @calls*)
          "the key and the dock pin land; the tryboot app keeps the screen"))))


(deftest a-quiet-storage-event-touches-nothing
  (let [calls* (atom [])]
    (with-redefs [systemd/active?    (constantly false)
                  app/release! (fn [] nil)
                  catalog/merge-app! (fn [& _] (swap! calls* conj :entry))
                  app/run!           (fn [& _] (swap! calls* conj :run))]
      (is (nil? (token/on-storage! (mounted secret) (mounted secret))))
      (is (= [] @calls*) "the token was already there — no workspace switch"))))


;; --- the eject grace --------------------------------------------------------

(deftest a-departure-that-stays-gone-closes-the-console
  (let [stopped* (atom [])]
    (with-redefs [token/eject-grace-ms 60
                  systemd/active?      (constantly false)
                  app/release! (fn [] nil)
                  catalog/merge-app!  (fn [& _] nil)
                  app/run!             (fn [& _] nil)
                  systemd/stop!        (fn [id] (swap! stopped* conj id))]
      (is (= :close (token/on-storage! [] (mounted secret))))
      (Thread/sleep 200)
      (is (= [:console] @stopped*)))))


(deftest a-reinsert-inside-the-grace-cancels-the-close
  (let [stopped* (atom [])]
    (with-redefs [token/eject-grace-ms 60
                  systemd/active?      (constantly false)
                  app/release! (fn [] nil)
                  catalog/merge-app!  (fn [& _] nil)
                  app/run!             (fn [& _] nil)
                  systemd/stop!        (fn [id] (swap! stopped* conj id))]
      (token/on-storage! [] (mounted secret))            ; yanked -> close armed
      (Thread/sleep 20)
      (token/on-storage! (mounted secret) [])            ; back before the grace ran out
      (Thread/sleep 200)
      (is (= [] @stopped*)
          "a yank is not always an eject — the armed close must become a no-op"))))


(deftest a-flapping-stick-does-not-yank-the-workspace
  (let [calls* (atom [])]
    (with-redefs [token/eject-grace-ms 5000                  ; the close never gets to fire
                  systemd/active?      (constantly true)     ; the console is still up
                  app/release! (fn [] nil)
                  catalog/merge-app!  (fn [& _] nil)
                  app/run!             (fn [id] (swap! calls* conj id))]
      (token/on-storage! [] (mounted secret))                ; bad contact drops it
      (is (= :open (token/on-storage! (mounted secret) []))) ; and it comes straight back
      (is (= [] @calls*)
          "app/run! switches workspace before its own gate, so calling it here would steal
           focus from whoever is typing, once per flap"))))


(deftest a-stick-from-another-circle-is-ignored
  (let [calls* (atom [])
        other  {:type :circle/secret :value {:key "not-ours"}}]
    (with-redefs [systemd/active? (constantly false)
                  app/release! (fn [] nil)
                  catalog/merge-app! (fn [& _] (swap! calls* conj :update))
                  app/run!        (fn [& _] (swap! calls* conj :run))]
      (is (nil? (token/on-storage! (mounted other) nil)))
      (is (empty? @calls*) "no env reaches the console, and it never launches")
      (is (= :open (token/on-storage! (mounted secret) (mounted other)))
          "our own key arriving after a foreign one still opens"))))
